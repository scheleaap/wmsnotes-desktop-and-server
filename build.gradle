buildscript {
    ext.kotlinVersion = '1.2.61'
    ext.grpcVersion = '1.15.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

repositories {
    mavenCentral()
    jcenter()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

apply plugin: 'application'

mainClassName = 'app.Application'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    // Infrastructure
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.2.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'au.com.console:kassava:1.0.0'

    // TornadoFX
    compile 'no.tornado:tornadofx:1.7.15'

    // Storage: SQLite, RxJava-JDBC, serialization
    compile 'org.xerial:sqlite-jdbc:3.21.0.1'
    compile 'com.github.davidmoten:rxjava2-jdbc:0.1-RC38'
    compile 'com.esotericsoftware:kryo:5.0.0-RC1' // Selected based on https://github.com/eishay/jvm-serializers/wiki

    // gRPC
    compile "io.grpc:grpc-netty-shaded:$grpcVersion"
    compile "io.grpc:grpc-protobuf:$grpcVersion"
    compile "io.grpc:grpc-stub:$grpcVersion"

    // RxKotlin and RxKotlinFX
    compile 'com.github.thomasnield:rxkotlinfx:2.2.2'
    compile 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    // Controls
//    compile 'org.controlsfx:controlsfx:8.40.14'
//    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.9.1'

    // Testing
    testCompile 'junit:junit:4.12'
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

group 'info.maaskant.wmsnotes'
version '1.0.0-SNAPSHOT'

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'app.Application'
        )
    }
    from configurations.compile.collect { entry -> zipTree(entry) }
}

buildscript {
    ext.kotlinVersion = '1.2.61'
    ext.grpcVersion = '1.15.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

repositories {
    mavenCentral()
    jcenter()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

apply plugin: 'application'

mainClassName = 'app.Application'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    // Infrastructure
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'au.com.console:kassava:1.0.0'
    // See also https://medium.com/@Zhuinden/that-missing-guide-how-to-use-dagger2-ef116fbea97
    compile 'com.google.dagger:dagger:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.16'
    compile 'com.github.thomasnield:rxkotlinfx:2.2.2'
    compile 'commons-codec:commons-codec:1.11'

    // Storage: SQLite, RxJava-JDBC, serialization, MapDB
//    compile 'org.xerial:sqlite-jdbc:3.21.0.1'
//    compile 'com.github.davidmoten:rxjava2-jdbc:0.1-RC38'
    compile 'com.esotericsoftware:kryo:5.0.0-RC1' // Selected based on https://github.com/eishay/jvm-serializers/wiki
    compile 'org.mapdb:mapdb:3.0.7'

    // gRPC
    compile "io.grpc:grpc-netty-shaded:$grpcVersion"
    compile "io.grpc:grpc-protobuf:$grpcVersion"
    compile "io.grpc:grpc-stub:$grpcVersion"

    // JavaFX, TornadoFX, RxKotlinFX, ControlsFX
    compile 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    compile 'no.tornado:tornadofx:1.7.15'
    compile 'no.tornado:tornadofx-controlsfx:0.1'
//    compile 'org.controlsfx:controlsfx:8.40.14'
//    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.9.1'

    // Configuration
//    compile 'com.natpryce:konfig:1.6.10.0'

    // Testing
    testCompile("org.junit.jupiter:junit-jupiter-api:5.3.1")
    testCompile("org.junit.jupiter:junit-jupiter-params:5.3.1")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.3.1")
    testCompile("org.assertj:assertj-core:3.11.1")
    // See also https://blog.philipphauer.de/best-practices-unit-testing-kotlin/
    testImplementation 'io.mockk:mockk:1.8.8'
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

group 'info.maaskant.wmsnotes'
version '1.0.0-SNAPSHOT'

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'info.maaskant.wmsnotes.desktop.app.Application'
        )
    }
    from configurations.compile.collect { entry -> zipTree(entry) }
}
